unicode-language-id ="root" 
                    / unicode-language-subtag 
                      [sep unicode-script-subtag] 
                      [sep unicode-region-subtag]
                      *(sep unicode-variant-subtag)

sep = "-" / "_"


unicode-language-subtag = 2*3ALPHA            ; shortest ISO 639 code
                          ["-" extlang]       ; sometimes followed by
                                              ; extended language subtags
                        / 4ALPHA              ; or reserved for future use
                        / 5*8ALPHA            ; or registered language subtag

extlang = 3ALPHA                              ; selected ISO 639 codes
          *2("-" 3ALPHA)                      ; permanently reserved


unicode-script-subtag = 4ALPHA                ; ISO 15924 code

unicode-region-subtag = 2ALPHA                ; ISO 3166-1 code
                      / 3DIGIT                ; UN M.49 code

unicode-variant-subtag = 5*8alphanum          ; registered variants
                       / (DIGIT 3alphanum)


alphanum = (ALPHA / DIGIT)                    ; letters and numbers

